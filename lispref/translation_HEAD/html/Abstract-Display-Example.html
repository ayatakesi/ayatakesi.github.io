<!DOCTYPE html>
<html>
<!-- Created by GNU Texinfo 7.1, https://www.gnu.org/software/texinfo/ -->
<head>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<!-- This is the GNU Emacs Lisp Reference Manual
corresponding to Emacs version 30.0.91.

Copyright © 1990-1996, 1998-2024 Free Software Foundation, Inc.

Permission is granted to copy, distribute and/or modify this document under
the terms of the GNU Free Documentation License, Version 1.3 or any later
version published by the Free Software Foundation; with the Invariant
Sections being "GNU General Public License," with the Front-Cover Texts
being "A GNU Manual," and with the Back-Cover Texts as in (a) below.  A
copy of the license is included in the section entitled "GNU Free
Documentation License."

(a) The FSF's Back-Cover Text is: "You have the freedom to copy and modify
this GNU manual.  Buying copies from the FSF supports it in developing GNU
and promoting software freedom." -->
<title>Abstract Display Example (GNU Emacs Lisp Reference Manual)</title>

<meta name="description" content="Abstract Display Example (GNU Emacs Lisp Reference Manual)">
<meta name="keywords" content="Abstract Display Example (GNU Emacs Lisp Reference Manual)">
<meta name="resource-type" content="document">
<meta name="distribution" content="global">
<meta name="Generator" content="texi2any">
<meta name="date" content="October 6, 2024">
<meta name="viewport" content="width=device-width,initial-scale=1">

<link href="index.html" rel="start" title="Top">
<link href="Index.html" rel="index" title="Index">
<link href="index.html#SEC_Contents" rel="contents" title="Table of Contents">
<link href="Abstract-Display.html" rel="up" title="Abstract Display">
<link href="Abstract-Display-Functions.html" rel="prev" title="Abstract Display Functions">
<style type="text/css">
<!--
a.copiable-link {visibility: hidden; text-decoration: none; line-height: 0em}
div.example {margin-left: 3.2em}
span:hover a.copiable-link {visibility: visible}
-->
</style>


</head>

<body lang="en">
<div class="subsection-level-extent" id="Abstract-Display-Example">
<div class="nav-panel">
<p>
Previous: <a href="Abstract-Display-Functions.html" accesskey="p" rel="prev">抽象ディスプレーの関数</a>, Up: <a href="Abstract-Display.html" accesskey="u" rel="up">抽象的なディスプレー</a> &nbsp; [<a href="index.html#SEC_Contents" title="Table of contents" rel="contents">Contents</a>][<a href="Index.html" title="Index" rel="index">Index</a>]</p>
</div>
<hr>
<h4 class="subsection" id="Chou-Xiang-deisupurenoLi"><span>42.21.2 抽象ディスプレーの例<a class="copiable-link" href="#Chou-Xiang-deisupurenoLi"> &para;</a></span></h4>

<p>以下は3つの整数からなるベクターを表すバッファー内の領域である<em class="dfn">カラー構成(color
components)</em>表示をewocパッケージ内の関数を使用して、さまざまな方法で実装するシンプルな例です。
</p>
<div class="example">
<pre class="example-preformatted">(setq colorcomp-ewoc nil
      colorcomp-data nil
      colorcomp-mode-map nil
      colorcomp-labels [&quot;Red&quot; &quot;Green&quot; &quot;Blue&quot;])

(defun colorcomp-pp (data)
  (if data
      (let ((comp (aref colorcomp-data data)))
        (insert (aref colorcomp-labels data) &quot;\t: #x&quot;
                (format &quot;%02X&quot; comp) &quot; &quot;
                (make-string (ash comp -2) ?#) &quot;\n&quot;))
    (let ((cstr (format &quot;#%02X%02X%02X&quot;
                        (aref colorcomp-data 0)
                        (aref colorcomp-data 1)
                        (aref colorcomp-data 2)))
          (samp &quot; (sample text) &quot;))
      (insert &quot;Color\t: &quot;
              (propertize samp 'face
                          `(foreground-color . ,cstr))
              (propertize samp 'face
                          `(background-color . ,cstr))
              &quot;\n&quot;))))

(defun colorcomp (color)
  &quot;新たなバッファー内でCOLORの編集を許可する。
そのバッファーはColor Componentsモードになる。&quot;
  (interactive &quot;sColor (name or #RGB or #RRGGBB): &quot;)
  (when (string= &quot;&quot; color)
    (setq color &quot;green&quot;))
  (unless (color-values color)
    (error &quot;No such color: %S&quot; color))
  (switch-to-buffer
   (generate-new-buffer (format &quot;originally: %s&quot; color)))
  (kill-all-local-variables)
  (setq major-mode 'colorcomp-mode
        mode-name &quot;Color Components&quot;)
  (use-local-map colorcomp-mode-map)
  (erase-buffer)
  (buffer-disable-undo)
  (let ((data (apply 'vector (mapcar (lambda (n) (ash n -8))
                                     (color-values color))))
        (ewoc (ewoc-create 'colorcomp-pp
                           &quot;\nColor Components\n\n&quot;
                           (substitute-command-keys
                            &quot;\n\\{colorcomp-mode-map}&quot;))))
    (set (make-local-variable 'colorcomp-data) data)
    (set (make-local-variable 'colorcomp-ewoc) ewoc)
    (ewoc-enter-last ewoc 0)
    (ewoc-enter-last ewoc 1)
    (ewoc-enter-last ewoc 2)
    (ewoc-enter-last ewoc nil)))
</pre></div>

<a class="index-entry-id" id="index-controller-part_002c-model_002fview_002fcontroller"></a>
<p>この例は<code class="code">colorcomp-data</code>の変更して選択プロセスを&ldquo;完了&rdquo;して、それらを互いに簡便に結ぶキーマップを定義することにより(言い換えると&ldquo;model/view/controller&rdquo;デザインパラダイムのcontroller部分)、&ldquo;color
selection widget&rdquo;への拡張が可能です。
</p>
<div class="example smallexample">
<pre class="example-preformatted">(defun colorcomp-mod (index limit delta)
  (let ((cur (aref colorcomp-data index)))
    (unless (= limit cur)
      (aset colorcomp-data index (+ cur delta)))
    (ewoc-invalidate
     colorcomp-ewoc
     (ewoc-nth colorcomp-ewoc index)
     (ewoc-nth colorcomp-ewoc -1))))

(defun colorcomp-R-more () (interactive) (colorcomp-mod 0 255 1))
(defun colorcomp-G-more () (interactive) (colorcomp-mod 1 255 1))
(defun colorcomp-B-more () (interactive) (colorcomp-mod 2 255 1))
(defun colorcomp-R-less () (interactive) (colorcomp-mod 0 0 -1))
(defun colorcomp-G-less () (interactive) (colorcomp-mod 1 0 -1))
(defun colorcomp-B-less () (interactive) (colorcomp-mod 2 0 -1))

(defun colorcomp-copy-as-kill-and-exit ()
  &quot;color componentsをkillリングにコピーしてバッファーをkill。
文字列は#RRGGBB(6桁16進が付加されたハッシュ)にフォーマットされる。&quot;
  (interactive)
  (kill-new (format &quot;#%02X%02X%02X&quot;
                    (aref colorcomp-data 0)
                    (aref colorcomp-data 1)
                    (aref colorcomp-data 2)))
  (kill-buffer nil))

(setq colorcomp-mode-map
      (define-keymap :suppress t
        &quot;i&quot; 'colorcomp-R-less
        &quot;o&quot; 'colorcomp-R-more
        &quot;k&quot; 'colorcomp-G-less
        &quot;l&quot; 'colorcomp-G-more
        &quot;,&quot; 'colorcomp-B-less
        &quot;.&quot; 'colorcomp-B-more
        &quot;SPC&quot; 'colorcomp-copy-as-kill-and-exit))
</pre></div>

<p>わたしたちが決して各ノード内のデータを変更していないことに注意してください。それらのデータはewoc作成時に<code class="code">nil</code>、または実際のカラーコンポーネントであるベクター<code class="code">colorcomp-data</code>にたいするインデックスに固定されています。
</p>
</div>
<hr>
<div class="nav-panel">
<p>
Previous: <a href="Abstract-Display-Functions.html">抽象ディスプレーの関数</a>, Up: <a href="Abstract-Display.html">抽象的なディスプレー</a> &nbsp; [<a href="index.html#SEC_Contents" title="Table of contents" rel="contents">Contents</a>][<a href="Index.html" title="Index" rel="index">Index</a>]</p>
</div>


This page has generated for branch:work/emacs-30_11e7ae3964e192b0e4bcc437a04278ee727e720b, commit:0e90041de618ad3a0245e73179829d0bdf8775bd to check Japanese translation.
</body>
</html>
